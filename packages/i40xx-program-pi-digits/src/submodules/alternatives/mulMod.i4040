%include "memory.i4040"
%include "mulMulti.i4040"
%include "divMulti.i4040"

# INPUT:
#   acc - variable number (06..0B)
# OUTPUT:
#   rr0, rr1, rr2, rr3 - variable content
# SIDE EFFECTS:
#   clears carry flag
loadVarFromMainMemory:
  XCH rr4
  CLB
  XCH rr5
  SRC r2
  RDM
  XCH rr0
  INC rr5
  SRC r2
  RDM
  XCH rr1
  INC rr5
  SRC r2
  RDM
  XCH rr2
  INC rr5
  SRC r2
  RDM
  XCH rr3
  BBL 0

# INPUT:
#   rr15 - variable number for first term and for result
#   rr14 - variable number for second term
#   memStatusCharVar[rr15] - first term (a)
#   memStatusCharVar[rr14] - second term (b)
#   STATUS_MEM_VARIABLE_MODULUS - divisor
# OUTPUT:
#   memStatusCharVar[rr15] - result
mulMod:
  # copy first term to mainMem[0x1]
  LD rr15
  JMS loadVarFromStatusCharMemory
  LDM 0x1
  JMS storeVarToMainMemory

  LD rr15
  WR0

  # copy second term to mainMem[0x2]
  LD rr14
  JMS loadVarFromStatusCharMemory
  LDM 0x2
  JMS storeVarToMainMemory

  FIM r1, 0x10
  FIM r2, 0x20
  JMS mulMWxMW

  LDM $STATUS_MEM_VARIABLE_MODULUS
  JMS loadVarFromStatusCharMemory
  LDM $MAIN_MEM_VARIABLE_DIV_DIVISOR
  JMS storeVarToMainMemory

  JMS divMWxMW

  LDM $MAIN_MEM_VARIABLE_DIV_REMINDER
  JMS loadVarFromMainMemory

  FIM r2, 0x10
  SRC r2
  RD0
  JMS storeVarToStatusCharMemory

  BBL 0
