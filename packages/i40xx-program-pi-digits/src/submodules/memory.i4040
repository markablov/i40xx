%define BANK_WITH_MUL_TABLE_FOR_FACTOR_3            0x1
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_4            0x1
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_5            0x2
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_6            0x2
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_7            0x3
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_9            0x3
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_A            0x4
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_B            0x4
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_C            0x5
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_D            0x5
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_E            0x6
%define BANK_WITH_MUL_TABLE_FOR_FACTOR_F            0x6
%define BANK_WITH_VARIABLES                         0x7

%define STATUS_MEM_VARIABLE_STARTING_PI_DIGITS_POSITION               0x1
%define STATUS_MEM_VARIABLE_POWER_10_EXPONENT                         0x2
%define STATUS_MEM_VARIABLE_F_COMPUTATION_ZERO_V_MULTIPLIER_B         0x2
%define STATUS_MEM_VARIABLE_F_COMPUTATION_REDUCED_COEF_B              0x3
%define STATUS_MEM_VARIABLE_MODULUS                                   0x4
%define STATUS_MEM_VARIABLE_DOUBLED_N_NEG                             0x5
%define STATUS_MEM_VARIABLE_DIGITS_COUNT                              0x5
%define STATUS_MEM_VARIABLE_CHUNKS_COUNT_NEG                          0x6
%define STATUS_MEM_VARIABLE_V                                         0x6
%define STATUS_MEM_VARIABLE_VMAX                                      0x6
%define STATUS_MEM_VARIABLE_MODULUS_NEG                               0x7
%define STATUS_MEM_VARIABLE_A_COMPUTATION_COEF                        0x8
%define STATUS_MEM_VARIABLE_B_COMPUTATION_COEF                        0x8
%define STATUS_MEM_VARIABLE_F_COMPUTATION_FK                          0x8
%define STATUS_MEM_VARIABLE_F_COMPUTATION_MULTIPLIER_A                0x8
%define STATUS_MEM_VARIABLE_F_COMPUTATION_MULTIPLIER_B                0x8
%define STATUS_MEM_VARIABLE_CURRENT_PRIME                             0x9
%define STATUS_MEM_VARIABLE_F_COMPUTATION_ZERO_V_LOOP_LIMIT           0x9
%define STATUS_MEM_VARIABLE_UPDATE_DIGITS_POWERED_L                   0x9
%define STATUS_MEM_VARIABLE_F_COMPUTATION_REDUCED_COEF_A              0xA
%define STATUS_MEM_VARIABLE_F_COMPUTATION_ZERO_V_MULTIPLIER_B_FACTOR  0xA
%define STATUS_MEM_VARIABLE_CURRENT_PRIME_POW                         0xA
%define STATUS_MEM_VARIABLE_UPDATE_DIGITS_CHUNK_NO                    0xA
%define STATUS_MEM_VARIABLE_UPDATE_DIGITS_CHUNK_COUNTER               0xA
%define STATUS_MEM_VARIABLE_F                                         0xB
%define STATUS_MEM_VARIABLE_F_COMPUTATION_A                           0xC
%define STATUS_MEM_VARIABLE_F_COMPUTATION_K                           0xD
%define STATUS_MEM_VARIABLE_F_COMPUTATION_K_NEG                       0xD
%define STATUS_MEM_VARIABLE_N_NEG                                     0xE
%define STATUS_MEM_VARIABLE_F_COMPUTATION_B                           0xF

%define STATUS_MEM_VARIABLE_DIGIT_B_COMPUTATION_COEF  8
%define STATUS_MEM_VARIABLE_DIGIT_A_COMPUTATION_COEF  8
%define STATUS_MEM_VARIABLE_DIGIT_F_COMPUTATION_FK    8
%define STATUS_MEM_VARIABLE_DIGIT_F_COMPUTATION_A     C
%define STATUS_MEM_VARIABLE_DIGIT_F_COMPUTATION_B     F

%define MAIN_MEM_VARIABLE_PRIME_SEGMENT_SIZE    0x2
%define MAIN_MEM_VARIABLE_PRIMENESS_MAP_START   0x4
%define MAIN_MEM_VARIABLE_DIV_REMINDER          0x8
%define MAIN_MEM_VARIABLE_DIV_DIVIDEND          0x8
%define MAIN_MEM_VARIABLE_DIV_DIVISOR           0x9
%define MAIN_MEM_VARIABLE_DIV_QUOTIENT          0xA
%define MAIN_MEM_VARIABLE_INITIAL_SEGMENT_START 0xC

%define MAIN_MEM_VARIABLE_DIGIT_PRIMENESS_MAP_START   0x4

clearMemory:
  FIM r0, 0x80
clearMemory_loopBank:
  LD rr1
  DCL
  LDM 0
  FIM r1, 0x00
clearMemory_loopRegAndChar:
  SRC r1
  WRM
  ISZ rr3, clearMemory_loopRegAndChar
  WR0
  WR1
  WR2
  WR3
  ISZ rr2, clearMemory_loopRegAndChar
  INC rr1
  ISZ rr0, clearMemory_loopBank
  BBL 0

# INPUT:
#   acc - variable number (06..0F)
#   rr0, rr1, rr2, rr3 - variable content
storeVarToStatusCharMemory:
  XCH rr4
  SRC r2
  LD rr0
  WR0
  LD rr1
  WR1
  LD rr2
  WR2
  LD rr3
  WR3
  BBL 0

# INPUT:
#   acc - variable number (06..0F)
# OUTPUT:
#   rr0, rr1, rr2, rr3 - variable content
loadVarFromStatusCharMemory:
  XCH rr0
  SRC r0
  RD0
  XCH rr0
  RD1
  XCH rr1
  RD2
  XCH rr2
  RD3
  XCH rr3
  BBL 0

# INPUT:
#   acc - variable number (06..0F)
clearVarFromStatusCharMemory:
  XCH rr0
  SRC r0
  CLB
  WR0
  WR1
  WR2
  WR3
  BBL 0

# INPUT:
#   acc - variable number (06..0B)
#   rr0, rr1, rr2, rr3 - variable content
# NOTES:
#   clears carry flag
storeVarToMainMemory:
  XCH rr4
  CLB
  XCH rr5
  SRC r2
  LD rr0
  WRM
  INC rr5
  SRC r2
  LD rr1
  WRM
  INC rr5
  SRC r2
  LD rr2
  WRM
  INC rr5
  SRC r2
  LD rr3
  WRM
  BBL 0

# INPUT:
#   acc - variable number (06..0B)
clearVarFromMainMemory:
  FIM r0, 0x00
  XCH rr0
clearVarFromMainMemory_loop:
  SRC r0
  WRM
  ISZ rr1, clearVarFromMainMemory_loop
  BBL 0

# INPUT:
#   acc - variable number (06..0B)
# OUTPUT:
#   rr1 - index of most significant word for variable
#   carry flag is set if variable is zero
MSWForVarFromMainMemory:
  FIM r0, 0x0F
  XCH rr0
MSWForVarFromMainMemory_loop:
  SRC r0
  RDM
  JCN nz, MSWForVarFromMainMemory_return
  LD rr1
  DAC
  CMC
  XCH rr1
  JCN nc, MSWForVarFromMainMemory_loop
MSWForVarFromMainMemory_return:
  BBL 0
