%include "memory.i4040"

# INPUT:
#   rr15 - variable number for first term and for result
#   rr14 - variable number for second term
#   memStatusCharVar[rr15] - first term (a)
#   memStatusCharVar[rr14] - second term (b)
#   STATUS_MEM_VARIABLE_MODULUS_INV
# OUTPUT:
#   memStatusCharVar[rr15] - result
# VARIABLES:
#   rr0..rr3 - 0x10000 - m
#   rr4..rr7 - b
#   rr8..rr11 - multipliedFactor
# NOTES:
#   - uses all registers
#   - clears carry flag
#   - first term should be always less than m
#   - second term is zero only if first term is zero too, so single check for fast path is enough
mulMod:
  LDM $STATUS_MEM_VARIABLE_MODULUS_INV
  JMS loadVarFromStatusCharMemory        # rr0..rr3 = 0x10000 - m

  SRC r7

  # acc is zero after loadVarFromStatusCharMemory() call, so after two XCH's we got rr14 = rr15 from input, rr15 = 0x0
  # by doing that we makes sure that after next SRC command, character #0 would be selected, which is good for some
  # operations later (outside that routine)
  XCH rr15
  XCH rr14

  RD0
  XCH rr6
  RD1
  XCH rr7
  RD2
  XCH rr4
  RD3
  XCH rr5                                # rr6 = b[0], rr7 = b[1], rr4 = b[2], rr5 = b[3]

  SRC r7
  RD0
  XCH rr8
  RD1
  XCH rr9
  RD2
  XCH rr10
  RD3
  XCH rr11                               # multipliedFactor =  a

  CLB
  WR0
  WR1
  WR2
  WR3                                    # res = 0

  LD rr6
  JMS mulMod_processNibble

  LD rr7
  OR4
  OR5
  JCN z, mulMod_ret

  LD rr7
  JMS mulMod_processNibble

  LD rr4
  OR5
  JCN z, mulMod_ret

  LD rr4
  JMS mulMod_processNibble

  LD rr5
  JCN z, mulMod_ret
  # can't do "JCN nz, mulMod_processNibble" because it would be short call, but ROM page with mulMod_processNibble would not fit main body
  JMS mulMod_processNibble

mulMod_ret:
  BBL 0

__location(0x3:0x00)
mulMod_processNibble_0:                   # 0b0000
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JUN mulMod_updateMultipliedFactor

__location(0x3:0x8)
mulMod_processNibble:
  XCH rr12
  CLB
  XCH rr13
  JIN r6

__location(0x3:0x10)
mulMod_processNibble_1:                   # 0b0001
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JUN mulMod_updateMultipliedFactor

__location(0x3:0x20)
mulMod_processNibble_2:                   # 0b0010
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JUN mulMod_updateMultipliedFactor

__location(0x3:0x30)
mulMod_processNibble_3:                   # 0b0011
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JUN mulMod_updateMultipliedFactor

__location(0x3:0x40)
mulMod_processNibble_4:                   # 0b0100
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JUN mulMod_updateMultipliedFactor

__location(0x3:0x50)
mulMod_processNibble_5:                   # 0b0101
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JUN mulMod_updateMultipliedFactor

__location(0x3:0x60)
mulMod_processNibble_6:                   # 0b0110
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JUN mulMod_updateMultipliedFactor

__location(0x3:0x70)
mulMod_processNibble_7:                   # 0b0111
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JUN mulMod_updateMultipliedFactor

__location(0x3:0x80)
mulMod_processNibble_8:                   # 0b1000
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JUN mulMod_updateMultipliedFactor

__location(0x3:0x90)
mulMod_processNibble_9:                   # 0b1001
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JUN mulMod_updateMultipliedFactor

__location(0x3:0xA0)
mulMod_processNibble_A:                   # 0b1010
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JUN mulMod_updateMultipliedFactor

__location(0x3:0xB0)
mulMod_processNibble_B:                   # 0b1011
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JUN mulMod_updateMultipliedFactor

__location(0x3:0xC0)
mulMod_processNibble_C:                   # 0b1100
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JUN mulMod_updateMultipliedFactor

__location(0x3:0xD0)
mulMod_processNibble_D:                   # 0b1101
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JUN mulMod_updateMultipliedFactor

__location(0x3:0xE0)
mulMod_processNibble_E:                   # 0b1110
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JUN mulMod_updateMultipliedFactor

__location(0x3:0xF0)
mulMod_processNibble_F:                   # 0b1111
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JMS mulMod_updateMultipliedFactor
  JMS mulMod_updateResult
  JUN mulMod_updateMultipliedFactor

# INPUT:
#   rr0..rr3 - m
#   rr8..rr11 - multipliedFactor
# OUTPUT:
#   rr8..rr11 - multipliedFactor
# VARIABLES:
#   rr12..rr15 - tmp
mulMod_updateMultipliedFactor:
  LD rr8
  ADD rr8
  XCH rr8
  LD rr9
  ADD rr9
  XCH rr9
  LD rr10
  ADD rr10
  XCH rr10
  LD rr11
  ADD rr11
  XCH rr11                                          # multipliedFactor = multipliedFactor + multipliedFactor

  LD rr8
  ADD rr0
  XCH rr12
  LD rr9
  ADD rr1
  XCH rr13
  LD rr10
  ADD rr2
  XCH rr14
  LD rr11
  ADD rr3
  XCH rr15                                          # tmp = multipliedFactor - m

  JCN nc, mulMod_updateMultipliedFactor_return      # if (tmp < 0)

  CLC
  LD rr12
  XCH rr8
  LD rr13
  XCH rr9
  LD rr14
  XCH rr10
  LD rr15
  XCH rr11                                          # multipliedFactor = tmp

mulMod_updateMultipliedFactor_return:
  BBL 0

# INPUT:
#   rr0..rr3 - m
#   rr8..rr11 - multipliedFactor
#   status characters in selected memory register - res
# OUTPUT:
#   status characters in selected memory register - res
# VARIABLES:
#   rr12..rr15 - tmp
# NOTES:
#   memory register already should be pre-selected
mulMod_updateResult:
  RD0
  ADD rr8
  WR0
  RD1
  ADD rr9
  WR1
  RD2
  ADD rr10
  WR2
  RD3
  ADD rr11
  WR3                                               # res = res + multipliedFactor

  RD0
  ADD rr0
  XCH rr12
  RD1
  ADD rr1
  XCH rr13
  RD2
  ADD rr2
  XCH rr14
  RD3
  ADD rr3
  XCH rr15                                          # tmp = res - m

  JCN nc, mulMod_updateResult_return                # if (tmp < 0)

  CLC
  LD rr12
  WR0
  LD rr13
  WR1
  LD rr14
  WR2
  LD rr15
  WR3                                               # res = tmp

mulMod_updateResult_return:
  BBL 0
