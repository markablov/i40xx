%include "submodules/memory.i4040"
%include "submodules/computePiDigits.i4040"
%include "submodules/mulTableGeneration.i4040"

main:
  JMS generateMulTables

  LDM $BANK_WITH_VARIABLES
  DCL

  LDM 3
  WMP                                                      # first Pi digit

  FIM r0, 0x00
  FIM r1, 0x00
  LDM $STATUS_MEM_VARIABLE_STARTING_PI_DIGITS_POSITION
  JMS storeVarToStatusCharMemory                           # startingPiDigitsPosition = 0

  FIM r0, 0x78
  FIM r1, 0x00
  LDM $STATUS_MEM_VARIABLE_DIGITS_COUNT
  JMS storeVarToStatusCharMemory                           # digitsCount = 135 = 0x87

  JUN computePiDigits

main_stage2:
  JMS clearMemory
  JMS generateMulTables
  LDM $BANK_WITH_VARIABLES
  DCL

  FIM r0, 0x78
  FIM r1, 0x00
  LDM $STATUS_MEM_VARIABLE_STARTING_PI_DIGITS_POSITION
  JMS storeVarToStatusCharMemory                           # startingPiDigitsPosition = 135 = 0x87

  FIM r0, 0x78
  FIM r1, 0x00
  LDM $STATUS_MEM_VARIABLE_DIGITS_COUNT
  JMS storeVarToStatusCharMemory                           # digitsCount = 135 = 0x87

  FIM r0, $MAIN_MEM_VARIABLE_COMPUTATION_STAGE . F
  SRC r0
  LDM 0x1
  WRM

  JUN computePiDigits

__rom_bank(0)
__location(00:0x02)
main_stage_end:
  LDM $BANK_WITH_VARIABLES
  DCL
  FIM r0, $MAIN_MEM_VARIABLE_COMPUTATION_STAGE . F
  SRC r0
  RDM
  JCN nz, main_end
  JUN main_stage2

__rom_bank(0)
__location(00:0x0C)
main_end:
  LDM 0x0F
  WMP                                                      # signal that job is done
  HLT

__rom_bank(0)
__location(00:0x00)
entrypoint:
  JUN main
