%include "memory.i4040"
%include "mulMod.i4040"
%include "shiftMulti.i4040"

# INPUT:
#   rr0..rr3 - base
#   MAIN_MEM_VARIABLE_POWER_MOD_EXP
#   MAIN_MEM_VARIABLE_M
# OUTPUT:
#   STATUS_MEM_VARIABLE_F_COMPUTATION_FK
# VARIABLES:
#   STATUS_MEM_VARIABLE_POWER_MOD_POWER_BASE
powerMod:
  LDM $STATUS_MEM_VARIABLE_POWER_MOD_POWER_BASE
  JMS storeVarToStatusCharMemory     # poweredBase = base
  LDM $STATUS_MEM_VARIABLE_F_COMPUTATION_FK
  JMS clearVarFromStatusCharMemory
  FIM r0, $STATUS_MEM_VARIABLE_F_COMPUTATION_FK . 0
  SRC r0
  LDM 0x01
  WR0                                # res = 1
powerMod_loop:
  FIM r0, $MAIN_MEM_VARIABLE_POWER_MOD_EXP . 0
  SRC r0
  RDM
  FIM r3, 0x10
  AN6
  JCN z, powerMod_skipResultUpdate  # if (exp & 1 === 0)
  FIM r7, $STATUS_MEM_VARIABLE_POWER_MOD_POWER_BASE . $STATUS_MEM_VARIABLE_DIGIT_F_COMPUTATION_FK
  JMS mulMod                         # res = (res * poweredBase) % m
powerMod_skipResultUpdate:
  FIM r7, $STATUS_MEM_VARIABLE_POWER_MOD_POWER_BASE . $STATUS_MEM_VARIABLE_DIGIT_POWER_MOD_POWER_BASE
  JMS mulMod                         # poweredBase = (poweredBase * poweredBase) % m
  LDM $MAIN_MEM_VARIABLE_POWER_MOD_EXP
  JMS MSWForVarFromMainMemory
  JCN c, powerMod_ret
  LD rr1
  XCH rr7
  FIM r0, $MAIN_MEM_VARIABLE_POWER_MOD_EXP . 0
  FIM r1, 0x13
  JMS shiftRightVarFromMainMemory   # exp = exp / 2
  LD rr12
  ADD rr1
  JCN c, powerMod_loop              # (exp.length - 1 + exp[0] === 0) => (exp.length === 1 && exp[0] === 0) => (exp === 0)
  JCN nz, powerMod_loop
powerMod_ret:
  CLC
  BBL 0
