%include "shift4.i4040"

# INPUT:
#   rr0 - variable number (06..0B)
#   rr1 - index of MSW
#   rr2 - shift value
#   rr3 - 4 - shift value
# VARIABLES:
#   rr13 - high shifted bits from previous digit
#   rr12 - low shifted bits from current digit
#   rr5 - loop iterator
# NOTES:
#   There is special case for long division with 16-word number dividend. This number ends with few 0s, so we perform
#   normalization step for such numbers by pretending that it has 17 digits, by safely replacing dividend[0] with
#   dividend[16]. This function should handle such case.
shiftLeftVarFromMainMemory:
  LDM 0x2
  XCH rr6
  LD rr1
  CMA
  JCN nz, shiftLeftVarFromMainMemory_normalVar
  IAC
  IAC
shiftLeftVarFromMainMemory_normalVar:
  XCH rr5                                   # rr5 = len(var) === 16 ? 2 : (16 - len(var))
  INC rr1
  INC rr1
shiftLeftVarFromMainMemory_loop:
  LD rr1
  SUB rr6
  CLC
  XCH rr1
  SRC r0
  RDM
  XCH rr12
  LD rr3
  JMS shiftRight                            # rr12 = var[i - 1] >> (4 - s)
  INC rr1
  SRC r0
  RDM
  XCH rr13
  LD rr2
  JMS shiftLeft                             # rr13 = var[i] << s
  LD rr13
  ADD rr12
  WRM                                       # var[i] = (var[i] << s) | (var[i - 1] >> (4 - s))
  ISZ rr5, shiftLeftVarFromMainMemory_loop
  LD rr1
  DAC
  CLC
  XCH rr1
  SRC r0
  RDM
  XCH rr13
  LD rr2
  JMS shiftLeft
  LD rr13                                   # var[0] = var[0] << s
  WRM
shiftLeftVarFromMainMemory_ret:
  BBL 0

# INPUT:
#   rr0 - variable number (06..0B)
#   rr1 - zero (modified to be variable digit index)
#   rr2 - shift value
#   rr3 - 4 - shift value
#   rr7 - MSW for variable
# VARIABLES:
#   rr5 - high shifted bits from previous digit
#   rr6 - low shifted bits from current digit
shiftRightVarFromMainMemory:
  LD rr7
  CMA
  IAC
  JCN z, shiftRightVarFromMainMemory_lastDigit
  XCH rr7
shiftRightVarFromMainMemory_loop:
  INC rr1
  SRC r0
  RDM
  XCH rr13
  LD rr3
  JMS shiftLeft                             # rr13 = var[digitIdx + 1] << (4 - shiftValue)
  LD rr1
  DAC
  CLC
  XCH rr1
  SRC r0
  RDM
  XCH rr12
  LD rr2
  JMS shiftRight                            # rr12 = var[digitIdx] >> shiftValue
  LD rr12
  ADD rr13
  WRM                                       # var[digitIdx] = rr12 | rr13
  INC rr1
  ISZ rr7, shiftRightVarFromMainMemory_loop
shiftRightVarFromMainMemory_lastDigit:
  SRC r0
  RDM
  XCH rr12
  LD rr2
  JMS shiftRight                            # rr12 = var[varDigitsAmount - 1] >> shiftValue
  LD rr12
  WRM
  BBL 0
